@export
Program = body:Block;

Block = {statements:Statement NewLine } [statements:Statement];

Statement = ( @:PrintStatement | @:EndStatement | @:Assignment );

NewLine = "\n";

# Expression

@export
@leftrec
Expression = @:Add | @:Sub | @:Term;
Add = left:*Expression '+'  right:Term;
Sub = left:*Expression '-' right:Term;
@leftrec
Term = @:Mul | @:Div | @:Factor;
Mul = left:*Term '*' right:Factor;
Div = left:*Term '/' right:Factor;
@memoize
Factor = @:Group | @:NumberLiteral | @:Variable;
Group = '(' body:*Expression ')';


# ASSIGNMENT
Assignment = variable:Variable "=" ( value:Expression | value:StringLiteral );

# PRINT
PrintStatement = KWPrint { list:PrintListItem };
PrintSkipNl = ";";
PrintListItem = (value:Expression | value:StringLiteral) [sep:PrintListSep];
PrintListSep = ";";

# END
EndStatement  = KWEnd;

# KEYWORDS
KWPrint = 'PRINT';
KWEnd = 'END';

# Variable
Variable = name:VariableName;

@string
VariableName = ( >ALPHA | "_" | "`" ) { >ALPHA | >DIGIT | "_" | "%" | "$" };

# LITERALS
NumberSign = '+' | '-';
NumberLiteral = value:FloatLiteral | value:IntegerLiteral;

@string
IntegerLiteral = [NumberSign] {'0'..'9'}+;

@string
FloatLiteral = [NumberSign] { >DIGIT }+ "." { >DIGIT} [ 'E' { >DIGIT }+ ];

@no_skip_ws
StringLiteral = '"' body:StringBody '"';

@string
StringBody = {!'"' char};


# Basics
ALPHA = 'a'..'z' | 'A'..'Z';
DIGIT = '0'..'9';

@no_skip_ws
Whitespace = {" "};