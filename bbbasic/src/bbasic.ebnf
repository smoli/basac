@export
Program = body:Block;

Block = {statements:Statement};

Statement = @:PrintStatement | @:EndStatement | @:Assignment;

# Expression

@export
@leftrec
Expression = @:Add | @:Sub | @:Term;
Add = left:*Expression '+'  right:Term;
Sub = left:*Expression '-' right:Term;
@leftrec
Term = @:Mul | @:Div | @:Factor;
Mul = left:*Term '*' right:Factor;
Div = left:*Term '/' right:Factor;
@memoize
Factor = @:Group | @:NumberLiteral | @:VariableName;
Group = '(' body:*Expression ')';


# ASSIGNMENT
Assignment = variable:VariableName "=" ( value:Expression | value:StringLiteral );

# PRINT
PrintStatement = KWPrint ( list:Expression | list:StringLiteral );

# END
EndStatement  = KWEnd;

# KEYWORDS
KWPrint = 'PRINT';
KWEnd = 'END';

# Variable
@string
VariableName = ( >ALPHA | "_" | "`" ) { >ALPHA | >DIGIT | "_" | "%" | "$" };

# LITERALS
NumberSign = '+' | '-';
NumberLiteral = value:FloatLiteral | value:IntegerLiteral;

@string
IntegerLiteral = [NumberSign] {'0'..'9'}+;

@string
FloatLiteral = [NumberSign] { >DIGIT }+ "." { >DIGIT} [ 'E' { >DIGIT }+ ];

@no_skip_ws
StringLiteral = '"' body:StringBody '"';

@string
StringBody = {!'"' char};


# Basics
ALPHA = 'a'..'z' | 'A'..'Z';
DIGIT = '0'..'9';