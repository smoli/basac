@export
Program = body:Block $;

Block = {statements:Statement {NewLine} } [statements:Statement];

Statement = ( @:IfStatement | @:ForStatement | @:PrintStatement | @:EndStatement | @:Assignment  );

NewLine = "\n";

# Expression

@export
@leftrec
Expression = @:Add | @:Sub | @:Term;
Add = left:*Expression '+'  right:Term;
Sub = left:*Expression '-' right:Term;
@leftrec
Term = @:Mul | @:Div | @:Factor;
Mul = left:*Term '*' right:Factor;
Div = left:*Term '/' right:Factor;
@memoize
Factor = @:Group | @:NumberLiteral | @:Variable;
Group = '(' body:*Expression ')';


# Bool Expression
@export
BoolExpression = disjunction:BoolDisjunction;
BoolDisjunction = conjunction:BoolConjunction { "OR" conjunction:BoolConjunction };
BoolConjunction = term:BoolTerm { "AND" term:BoolTerm };
BoolTerm = ("(" expression:BoolExpression ")") | condition:BoolCondition;
BoolCondition = left:BoolOperand op:BoolOperator right:BoolOperand;
BoolOperator = @:BoolOpNotEqual | @:BoolOpEqual | @:BoolOpLowerEqual | @:BoolOpGreaterEqual | @:BoolOpLower | @:BoolOpGreater;
BoolOperand = @:Expression | @:StringLiteral;
BoolOpNotEqual = "<>";
BoolOpEqual = "=";
BoolOpLowerEqual = "<=";
BoolOpGreaterEqual = ">=";
BoolOpLower = "<";
BoolOpGreater = ">";

# IF

ThenBlock = @:Block;
ElseBlock = @:Block;
IfCondition = @:BoolDisjunction;
IfStatement = KWIf condition:IfCondition KWThen {NewLine}+ thenBlock:ThenBlock [{NewLine} KWElse {NewLine}+ elseBlock:ElseBlock] {NewLine} KWEndif;


# ASSIGNMENT
Assignment = variable:Variable "=" ( value:Expression | value:StringLiteral );

# PRINT
PrintStatement = KWPrint { list:PrintListItem };
PrintSkipNl = ";";
PrintListItem = (value:Expression | value:StringLiteral) [sep:PrintListSep];
PrintListSep = ";";

# FOR
ForStatement = KWFor assignment:Assignment KWTo target:Expression [step:ForStep] NewLine body:Block next:ForNext;
#ForStatement = KWFor assignment:Assignment KWTo target:Expression;
ForStep = KWStep value:Expression;
ForNext = KWNext [Variable];


# END
EndStatement  = KWEnd NewLine;

# KEYWORDS
KWPrint = 'PRINT';
KWFor = 'FOR';
KWTo = 'TO';
KWStep = 'STEP';
KWNext = 'NEXT';
KWEnd = 'END';
KWIf = 'IF';
KWThen = 'THEN';
KWElse = 'ELSE';
KWEndif = 'ENDIF';

# Variable
Variable = name:VariableName;

@no_skip_ws
@string
VariableName = ( >ALPHA | "_" | "`" ) { >ALPHA | >DIGIT | "_" | "%" | "$" };

# LITERALS
NumberSign = '+' | '-';
NumberLiteral = value:FloatLiteral | value:IntegerLiteral;

@no_skip_ws
@string
IntegerLiteral = [NumberSign] {'0'..'9'}+;

@no_skip_ws
@string
FloatLiteral = [NumberSign] { >DIGIT }+ "." { >DIGIT} [ 'E' { >DIGIT }+ ];

@no_skip_ws
StringLiteral = '"' body:StringBody '"';

@no_skip_ws
@string
StringBody = {!'"' char};


# Basics
ALPHA = 'a'..'z' | 'A'..'Z';
DIGIT = '0'..'9';

@no_skip_ws
Whitespace = {" "};